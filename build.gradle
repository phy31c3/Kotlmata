plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.4.21'
	id 'maven'
	id 'signing'
}

group = "kr.co.plasticcity"
archivesBaseName = "kotlmata"
version = "1.0.5"

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	implementation "org.jetbrains.kotlin:kotlin-reflect"
	testImplementation 'junit:junit:4.12'
}

compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
	kotlinOptions.jvmTarget = "1.8"
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Kotlmata'
				packaging 'jar'
				description 'Automata-based programming library for Kotlin.'
				url 'https://github.com/phy31c3/Kotlmata'

				scm {
					connection = 'scm:git:github.com/phy31c3/Kotlmata.git'
					developerConnection = 'scm:git:ssh://github.com/phy31c3/Kotlmata.git'
					url = 'https://github.com/phy31c3/Kotlmata'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'phy31c3'
						name 'Jongsun Yoo'
						email 'phy31c3@gmail.com'
					}
				}
			}
		}
	}
}
